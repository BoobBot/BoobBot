buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


plugins {
    id "idea"
    id "application"
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "org.jetbrains.kotlin.jvm" version "1.2.70"
}


group "bot.boobbot"
version getBuildVersion()
mainClassName = "bot.boobbot.BoobBot"


repositories {
    jcenter()
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url "https://dl.bintray.com/kotlin/ktor" }
}


dependencies {
    /* Discord Stuff */
    implementation ("net.dv8tion:JDA:$jda_version") {
        exclude module: 'opus-java'
    }
    implementation "club.minnced:discord-webhooks:$webhooks_version"

    /* Audio */
    implementation "com.sedmelluq:lavaplayer:$lp_version"
    implementation "com.sedmelluq:jda-nas:$nas_version"

    /* Kotlin Stuff */
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coro_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlin_coro_version"

    /* Logging */
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.sentry:sentry-logback:$sentry_version"
    implementation "org.codehaus.janino:janino:3.0.7"

    /* Webserver */
    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-websockets:$ktor_version"
    implementation "io.ktor:ktor-gson:$ktor_version"
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "com.google.code.gson:gson:2.8.6"

    /* Eval */
    implementation "org.jetbrains.kotlin:kotlin-script-runtime:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-script-util:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-scripting-jsr223:$kotlin_version"

    /* Misc */
    implementation "com.github.SvenWoltmann:color-thief-java:$color_thief_version"
    implementation "org.reflections:reflections:$reflections_version"
    implementation "io.github.cdimascio:java-dotenv:3.1.2"
    implementation "org.javadelight:delight-metrics:0.0.6"
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation 'org.mongodb:mongodb-driver-sync:3.10.1'
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

shadowJar {
    baseName = "bb"
    classifier = null
    version = null
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
}

def getBuildVersion() {
    def gitVersion = new ByteArrayOutputStream()
    exec {
        commandLine("git", "rev-parse", "--short", "HEAD")
        standardOutput = gitVersion
    }
    return gitVersion.toString().trim()
}

task writeVersion() {
    new File("src/main/resources", "version.txt").text = getBuildVersion()
}

build {
    dependsOn writeVersion
}
